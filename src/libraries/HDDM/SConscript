#
# Nov. 20, 2013  David Lawrence
#
# This is pretty ugly (they way we handle this in scons, not HDDM itself).
# The way HDDM is set up in the repository is the following:
#
# utilities used for generating files like hddm_s.c and hddm_s.h
# from the input event.xml have their source in:
# https://github.com/jeffersonlab/HDDM
#
# The HDDM library is made up of code maintained in the
# src/libraries/HDDM directory as well as code generated
# from XML files in that same directory using the above
# utilities.
#
# To actually generate the C/C++ source from the XML we create two builders
# (one for C and the other for C++) here. The builders run the utilities
# hddm-c and hddm-cpp from the external HDDM package, which are assumed
# to be found somewhere in the current PATH. To further complicate
# matters, the output of hddm-c is two files (e.g. hddm_s.c and hddm_s.h)
# which do not depend on the name of the input file ("event.xml"). We specify
# two products of the input XML, but the value that $TARGET gets set to
# in the builder is just the first of these. We choose to always use the header
# files as the first target so we can easily strip off the ".h" (or ".hpp")
# suffix to get the base name of the target. This is what is passed into
# the "-o" option of hddm-c as it will automatically add ".c" and ".h" to this.
#
# Another complication is that by default, the C++ files produced will
# have a ".cpp" suffix, but the same base name. This would normally lead
# to object files for hddm_s.c and hddm_s.cpp both being hddm_s.o.
# We follow the historical solution used in BMS of renaming the hddm_s.cpp
# to hddm_s++.cpp.


import os
import re
import subprocess
import SCons
import sbms

# get env object and clone it
Import('*')
env.Decider('MD5-timestamp')

#========================================================================
# Python functions used by the hddm-c and hddm-cpp builders

#---------------
# HDDM_C
#---------------
def HDDM_C(target, source, env):

	# Get basename with full path for target.
	# The first target should always be the header
	# file name to be generated so we just drop the
	# suffix from that.
	target_base = re.sub('\.h$', '', str(target[0]))

	# Form command to be executed and execute it
	cmd = ["hddm-c", '-o', target_base, str(source[0])]
	if( int(env['SHOWBUILD']) > 0): print(' '.join(cmd))
	cmdout = subprocess.Popen(cmd, stdout=subprocess.PIPE).communicate()

#---------------
# HDDM_CPP
#---------------
def HDDM_CPP(target, source, env):

	# Get basename with full path for target.
	# The first target should always be the header
	# file name to be generated so we just drop the
	# suffix from that.
	target_base = re.sub('\.hpp$', '', str(target[0]))

	# Form command to be executed and execute it
	cmd = ["hddm-cpp", '-o', target_base, str(source[0])]
	if( int(env['SHOWBUILD']) > 0): print(' '.join(cmd))
	cmdout = subprocess.Popen(cmd, stdout=subprocess.PIPE).communicate()

#---------------
# HDDM_PY
#---------------
def HDDM_PY(target, source, env):

    # separate the target directory and basename
	target_base = re.sub('\.cpy$', '', str(target[1]))

	# Form command to be executed and execute it
	cmd = ["hddm-py", '-o', target_base, str(source[0])]
	if( int(env['SHOWBUILD']) > 0): print(' '.join(cmd))
	cmdout = subprocess.Popen(cmd, stdout=subprocess.PIPE).communicate()
    

# end of Python functions. Global-level scons continues below
#========================================================================

# To get succinct output, define the builder actions based
# on the value of SHOWBUILD
if env['SHOWBUILD']==0:
	hddmcaction   = SCons.Script.Action(HDDM_C  , 'HDDM-C     [$SOURCE]')
	hddmcppaction = SCons.Script.Action(HDDM_CPP, 'HDDM-CPP   [$SOURCE]')
	hddmpyaction  = SCons.Script.Action(HDDM_PY,  'HDDM-PY    [$SOURCE]')
else:
	hddmcaction   = SCons.Script.Action(HDDM_C)
	hddmcppaction = SCons.Script.Action(HDDM_CPP)
	hddmpyaction  = SCons.Script.Action(HDDM_PY)

# Create the actual scons builders from the actions defined above
bldc   = SCons.Script.Builder(action = hddmcaction)
bldcpp = SCons.Script.Builder(action = hddmcppaction)
bldpy  = SCons.Script.Builder(action = hddmpyaction)
env.Append(BUILDERS = {'HDDMC'   : bldc})
env.Append(BUILDERS = {'HDDMCPP' : bldcpp})
env.Append(BUILDERS = {'HDDMPY'  : bldpy})

# Add the C/C++ HDDM (de)serializer source dependencies
# by hand. The output file names depend on the class
# tag defined inside the XML file. So, specifying the
# inputs and outputs explicitly here is just easier.
env.AppendUnique(HDDMC_SRC   = env.HDDMC(['hddm_s.h', 'hddm_s.c'], ['event.xml']))
env.AppendUnique(HDDMC_SRC   = env.HDDMC(['hddm_mc_s.h', 'hddm_mc_s.c'], ['mc.xml']))
env.AppendUnique(HDDMC_SRC   = env.HDDMC(['hddm_r.h', 'hddm_r.c'], ['rest.xml']))
env.AppendUnique(HDDMCPP_SRC = env.HDDMCPP(['hddm_s.hpp', 'hddm_s++.cpp'], ['event.xml']))
env.AppendUnique(HDDMCPP_SRC = env.HDDMCPP(['hddm_mc_s.hpp', 'hddm_mc_s++.cpp'], ['mc.xml']))
env.AppendUnique(HDDMCPP_SRC = env.HDDMCPP(['hddm_r.hpp', 'hddm_r++.cpp'], ['rest.xml']))
env.AppendUnique(HDDMPY_SRC  = env.HDDMPY(['setup_hddm_s.py', 'pyhddm_s.cpy'], ['event.xml']))
env.AppendUnique(HDDMPY_SRC  = env.HDDMPY(['setup_hddm_mc_s.py', 'pyhddm_mc_s.cpy'], ['mc.xml']))
env.AppendUnique(HDDMPY_SRC  = env.HDDMPY(['setup_hddm_r.py', 'pyhddm_r.cpy'], ['rest.xml']))

# Additional dependencies in the generated module source files
env.Depends(['setup_hddm_s.py', 'pyhddm_s.cpy'], ['hddm_s.hpp', 'hddm_s++.cpp'])
env.Depends(['setup_hddm_mc_s.py', 'pyhddm_mc_s.cpy'], ['hddm_mc_s.hpp', 'hddm_mc_s++.cpp'])
env.Depends(['setup_hddm_r.py', 'pyhddm_r.cpy'], ['hddm_r.hpp', 'hddm_r++.cpp'])

# Finally, clone the build environment and make a library
# out of all source. This should include the generated
# HDDM (de)serializer routines.
env = env.Clone()

sbms.AddDANA(env)
sbms.library(env)

# Add dependencies for the python modules as well
sbms.python_so_module(env, 'hddm_s')
sbms.python_so_module(env, 'hddm_mc_s')
sbms.python_so_module(env, 'hddm_r')


#========================================================================
# comment by RTJ, June 27, 2016
# With the introduction of a full implementation of a new python
# API for hddm classes, the swig wrapping of hddm is now deprecated.
# The original swig build instructions below are kept as comments for
# documentation purposes.
#
# end of comment by RTJ
#========================================================================
# now we try to build wrapper libraries - these are only built if the swig
# executable exists and that building these executables is enabled
# these are needed for other systems that work with HDDM files, e.g. EventStore
#sbms.AddSWIG(env)
#swig_env = env.Clone()
#swig_env.AppendUnique(SWIGFLAGS = ["-c++","-python"])
#swig_env.AppendUnique(LIBS = ["z","bz2"])
#sbms.swig_library(swig_env, "pyhddm_r", ["pyhddm_r.i"])
#sbms.swig_library(swig_env, "pyhddm_s", ["pyhddm_s.i"])
